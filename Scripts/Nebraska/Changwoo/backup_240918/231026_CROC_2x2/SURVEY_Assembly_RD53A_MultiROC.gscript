VERSION v2.1
######################## RD53A Multiple ROC Assembly #############################
# Steps:
# 1-12. Already done by BUILD script
# 13. Release vacuum of Vacuum Tool
# 14. Prompt replacement of Vacuum Tool on Chuck 3
# 15. Calculate the placement of the HDI and the ROC
# 16. Move gantry head to Chuck 1 and survey for Module accuracy
# 17. End program with vacuums on or off

#Initialize the system
HOME
  #Load needed variables from the flex_config
LOADCONFIG
FLEXREAD $dim_ROC geometry.TFPX_RD53AQUARTERROC.fid_br
FLEXREAD $dim_ROC.z geometry.TFPX_RD53AQUARTERROC.thickness
FLEXREAD $thickness_HDI geometry.TFPX_RD53AHDI.thickness
FLEXREAD $dim_bondHDI geometry.TFPX_RD53AHDI.dim_bondHDI
FLEXREAD $len_bondpad_HDI geometry.TFPX_RD53AHDI.len_bondpad_HDI
MUL $dim_bondHDI $dim_bondHDI {0.5,0.5,1}

  #Load the rest of the variables from the log file (from the first half of the script)
LOADCONFIG multi Logs\LOG_BUILD_Assembly_RD53A_MultiROC_DataGeneration.txt
FLEXREAD $pos_hdi_center_withError multi.pos_hdi_center_withError
FLEXREAD $rot_hdi_withError multi.rot_hdi_withError


## Continuing the script ##
# Step 13. Release vacuum of Vacuum Tool
@STEP_THIRTEEN PASS
SETVAC rd53a_chuck_3 0


# Step 14. Prompt replacement of Vacuum Tool on Chuck 3
@STEP_FOURTEEN CHOICEPOPUP $choice "Did you place the Vacuum Tool on Chuck 3?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_FOURTEEN $choice


# Step 15. Calculate the placement of the ROC and the HDI
@STEP_FIFTEEN PASS
  #ROC
CALL @CALCCORNERS $pos_hdi_center_withError $rot_hdi_withError $dim_ROC -> $pos_TR_ROC_center $pos_BR_ROC_center $pos_BL_ROC_center $pos_TL_ROC_center
CALL @CALCCORNERS $pos_TR_ROC_center $rot_hdi_withError $dim_ROC -> $pos_TR_ROC_tr $pos_TR_ROC_br $trash_bl $trash_tl
CALL @CALCCORNERS $pos_BR_ROC_center $rot_hdi_withError $dim_ROC -> $pos_BR_ROC_tr $pos_BR_ROC_br $trash_bl $trash_tl
CALL @CALCCORNERS $pos_BL_ROC_center $rot_hdi_withError $dim_ROC -> $trash_tr $trash_br $pos_BL_ROC_bl $pos_BL_ROC_tl
CALL @CALCCORNERS $pos_TL_ROC_center $rot_hdi_withError $dim_ROC -> $trash_tr $trash_br $pos_TL_ROC_bl $pos_TL_ROC_tl

  #HDI
SUB $pos_hdi_center_withError.z $pos_hdi_center_withError.z $thickness_HDI
CALL @CALCCORNERS $pos_hdi_center_withError $rot_hdi_withError $dim_bondHDI -> $pos_TRchuck1_center $pos_BRchuck1_center $pos_BLchuck1_center $pos_TLchuck1_center
CALL @CALCCORNERS $pos_TRchuck1_center $rot_hdi_withError $dim_bondHDI -> $pos_TRchuck1_tr $pos_TRchuck1_br $trash_bl $trash_tl
CALL @CALCCORNERS $pos_BRchuck1_center $rot_hdi_withError $dim_bondHDI -> $pos_BRchuck1_tr $pos_BRchuck1_br $trash_bl $trash_tl
CALL @CALCCORNERS $pos_BLchuck1_center $rot_hdi_withError $dim_bondHDI -> $trash_tr $trash_br $pos_BLchuck1_bl $pos_BLchuck1_tl
CALL @CALCCORNERS $pos_TLchuck1_center $rot_hdi_withError $dim_bondHDI -> $trash_tr $trash_br $pos_TLchuck1_bl $pos_TLchuck1_tl


# Step 16. Move gantry head to Chuck 1 and survey for Module accuracy
@STEP_SIXTEEN PASS
@ACC_0 CHOICEPOPUP $choice "Ready to survey the Top Right ROC's accuracy?"
GOTOIFN @ACC_0 $choice
CALL @QUARTERSURVEILLANCE $pos_TRchuck1_tr $pos_TRchuck1_br $pos_TR_ROC_tr $pos_TR_ROC_br -1 -> $TR_dx $TR_dy

SETLOG "LOG_SPLIT_Assembly_RD53A_MultiROC_DataGeneration.txt"
PRINT "---------------------"
PRINT "Top Right"
PRINT "Delta(delta(X)): %f" $TR_dx
PRINT "Avg(delta(Y)): %f" $TR_dy

@ACC_1 CHOICEPOPUP $choice "Ready to survey the Bottom Right ROC's accuracy?"
GOTOIFN @ACC_1 $choice
CALL @QUARTERSURVEILLANCE $pos_BRchuck1_tr $pos_BRchuck1_br $pos_BR_ROC_tr $pos_BR_ROC_br -1 -> $BR_dx $BR_dy

PRINT "---------------------"
PRINT "Bottom Right"
PRINT "Delta(delta(X)): %f" $BR_dx
PRINT "Avg(delta(Y)): %f" $BR_dy

@ACC_2 CHOICEPOPUP $choice "Ready to survey the Bottom Left ROC's accuracy?"
GOTOIFN @ACC_2 $choice
CALL @QUARTERSURVEILLANCE $pos_BLchuck1_tl $pos_BLchuck1_bl $pos_BL_ROC_tl $pos_BL_ROC_bl +1 -> $BL_dx $BL_dy

PRINT "---------------------"
PRINT "Bottom Left"
PRINT "Delta(delta(X)): %f" $BL_dx
PRINT "Avg(delta(Y)): %f" $BL_dy

@ACC_3 CHOICEPOPUP $choice "Ready to survey the Top Left ROC's accuracy?"
GOTOIFN @ACC_3 $choice
CALL @QUARTERSURVEILLANCE $pos_TLchuck1_tl $pos_TLchuck1_bl $pos_TL_ROC_tl $pos_TL_ROC_bl +1 -> $TL_dx $TL_dy

PRINT "---------------------"
PRINT "Top Left"
PRINT "Delta(delta(X)): %f" $TL_dx
PRINT "Avg(delta(Y)): %f" $TL_dy


# Step 17. End program with vacuums on or off
@STEP_SEVENTEEN PASS
CHOICEPOPUP $choice "Do you want to leave the vacuums on?" "Yes, Leave them on" "No, Turn them off"
GOTOIF @STEP_END $choice

SETVAC gantry_head_inner 0
SETVAC gantry_head_outer 0
SETVAC rd53a_chuck_1_TL 0
SETVAC rd53a_chuck_1_TR 0
SETVAC rd53a_chuck_1_BL 0
SETVAC rd53a_chuck_1_BR 0
SETVAC rd53a_chuck_2 0
SETVAC rd53a_chuck_3 0
SETVAC launchpad 0

@STEP_END HOME

END


# SURVEYCORNERS
# Arguments:
# $x_corner - Approximate position of an object that you would like to know the precise position. Returns with precise position written in this location
#
# Returns:
# $x_corner - Updated position of the corner that is being looked for
@SURVEYCORNERS(x_corner)
  MOVESAFE $x_corner 100
  @SURCORNPOP VIDEO
  CHOICEPOPUP $choice "Did you correctly align the cursor with the marker?" "Yes" "No"
  GOTOIFN @SURCORNPOP $choice
  GETPOS $x_corner
  RETURN $x_corner


# CALCCORNERS
# Arguments:
#  $center - center of object
#  $rotation - rotation of the object
#  $dimensions - dimensions of object (as a vector)
#
# Returns
#  $topRight - position of the top right corner
#  $botRight - position of the bottom right corner
#  $botLeft - position of the bottom left corner
#  $topLeft - position of the top left corner
@CALCCORNERS(center,rotation,dimensions)
  SIN $sin_angle $rotation
  COS $cos_angle $rotation
  COPY $width $dimensions.x
  COPY $length $dimensions.y
  MUL $w_cos $width $cos_angle
  MUL $w_sin $width $sin_angle
  MUL $l_cos $length $cos_angle
  MUL $l_sin $length $sin_angle

  ADD $d_x_TR $l_sin $w_cos
  ADD $topRight $center.x $d_x_TR
  SUB $d_y_TR $w_sin $l_cos
  ADD $topRight.y $center.y $d_y_TR
  COPY $topRight.z $center.z

  SUB $d_x_BR $w_cos $l_sin
  ADD $botRight $center.x $d_x_BR
  ADD $d_y_BR $w_sin $l_cos
  ADD $botRight.y $center.y $d_y_BR
  COPY $botRight.z $center.z

  ADD $d_x_BL $w_cos $l_sin
  SUB $botLeft $center.x $d_x_BL
  SUB $d_y_BL $l_cos $w_sin
  ADD $botLeft.y $center.y $d_y_BL
  COPY $botLeft.z $center.z

  SUB $d_x_TL $l_sin $w_cos
  ADD $topLeft $center.x $d_x_TL
  ADD $d_y_TL $l_cos $w_sin
  SUB $topLeft.y $center.y $d_y_TL
  COPY $topLeft.z $center.z

  RETURN $topRight $botRight $botLeft $topLeft


# QUARTERSURVEILLANCE
# Arguments:
#  $top_hdi - Position of the top HDI bond pad
#  $bot_hdi - Position of the bottom HDI bond pad
#  $top_roc - Position of the top ROC bond pad
#  $bot_roc - Position of the bottom ROC bond pad
#  $sign    - +1 for left side ROCs, -1 for right side ROCs
#
# Returns:
#  $d_DeltaX   - opening angle
#  $avg_DeltaY -
@QUARTERSURVEILLANCE(top_hdi,bot_hdi,top_roc,bot_roc,sign)
  MOVESAFE $top_hdi 100

  # HDI bond pads, top and bottom
  @HDI_TOP_OUTER CHOICEPOPUP $choice "Ready to survey the HDI's upper bond pad's outer edge's middle?"
  GOTOIFN @HDI_TOP_OUTER $choice
  CALL @SURVEYCORNERS  $top_hdi -> $top_hdi_outer
  COPY $top_hdi_inner $top_hdi_outer
  MUL $len_bondHDI_signed $len_bondpad_HDI $sign
  ADD $top_hdi_inner.x $top_hdi_outer.x $len_bondHDI_signed
  @HDI_TOP_INNER CHOICEPOPUP $choice "Ready to survey the HDI's upper bond pad's inner edge's middle?"
  GOTOIFN @HDI_TOP_INNER $choice
  CALL @SURVEYCORNERS $top_hdi_inner -> $top_hdi_inner
  ADD $top_hdi $top_hdi_outer $top_hdi_inner
  MUL $top_hdi $top_hdi {0.5,0.5,0.5}
  COPY $bot_hdi.z $top_hdi.z

  @HDI_BOT_OUTER CHOICEPOPUP $choice "Ready to survey the HDI's lower bond pad's outer edge's middle?"
  GOTOIFN @HDI_BOT_OUTER $choice
  CALL @SURVEYCORNERS  $bot_hdi -> $bot_hdi_outer
  COPY $bot_hdi_inner $bot_hdi_outer
  ADD $bot_hdi_inner.x $bot_hdi_outer.x $len_bondHDI_signed
  @HDI_BOT_INNER CHOICEPOPUP $choice "Ready to survey the HDI's lower bond pad's inner edge's middle?"
  GOTOIFN @HDI_BOT_INNER $choice
  CALL @SURVEYCORNERS $bot_hdi_inner -> $bot_hdi_inner
  ADD $bot_hdi $bot_hdi_outer $bot_hdi_inner
  MUL $bot_hdi $bot_hdi {0.5,0.5,0.5}

  @ROC_TOP_1 CHOICEPOPUP $choice "Ready to survey one corner of the ROC's top corner bond pad?"
  GOTOIFN @ROC_TOP_1 $choice
  CALL @SURVEYCORNERS $top_roc -> $top_roc_1
  @ROC_TOP_2 CHOICEPOPUP $choice "Ready to survey the opposite corner of the ROC's top corner bond pad?"
  GOTOIFN @ROC_TOP_2 $choice
  CALL @SURVEYCORNERS $top_roc_1 -> $top_roc_2
  ADD $top_roc $top_roc_1 $top_roc_2
  MUL $top_roc $top_roc {0.5,0.5,0.5}
  COPY $bot_roc.z $top_roc.z

  @ROC_BOT_1 CHOICEPOPUP $choice "Ready to survey one corner of the ROC's bottom corner bond pad?"
  GOTOIFN @ROC_BOT_1 $choice
  CALL @SURVEYCORNERS $bot_roc -> $bot_roc_1
  @ROC_BOT_2 CHOICEPOPUP $choice "Ready to survey the opposite corner of the ROC's bottom corner bond pad?"
  GOTOIFN @ROC_BOT_2 $choice
  CALL @SURVEYCORNERS $bot_roc_1 -> $bot_roc_2
  ADD $bot_roc $bot_roc_1 $bot_roc_2
  MUL $bot_roc $bot_roc {0.5,0.5,0.5}

  CALL @SURVEYCALC $top_hdi $bot_hdi $top_roc -> $d_upper_x $d_upper_y
  CALL @SURVEYCALC $bot_hdi $top_hdi $bot_roc -> $d_lower_x $d_lower_y

  SUB $d_DeltaX $d_upper_x $d_lower_x
  ADD $avg_DeltaY $d_upper_y $d_lower_y
  MUL $avg_DeltaY $avg_DeltaY 0.5
  RETURN $d_DeltaX $avg_DeltaY


# SURVEYCALC
# Arguments:
#  $near_hdi - Center position for the nearest HDI bond pad
#  $far_hdi - Center position for the furthest HDI bond pad
#  $ROC - Center position for the current ROC bond pad
#
# Returns:
#  $delta_x
#  $delta_y
@SURVEYCALC(near_hdi,far_hdi,ROC)
  #Variables
  COPY $a $near_hdi.x
  COPY $b $near_hdi.y
  COPY $c $far_hdi.x
  COPY $d $far_hdi.y
  COPY $e $ROC.x
  COPY $f $ROC.y

  #Slope
  SUB $b_min_d $b $d
  SUB $a_min_c $a $c
  INVERT $a_min_c $a_min_c
  MUL $slope $b_min_d $a_min_c

  #Calulating numerator for x
  MUL $slope_2 $slope $slope
  MUL $top_one $a $slope_2
  SUB $f_min_b $f $b
  MUL $top_two $f_min_b $slope
  ADD $top_oneTwo $top_one $top_two
  ADD $top $top_oneTwo $e

  #Calculating denominator for x
  ADD $bottom $slope_2 1
  INVERT $bottom $bottom

  #Calculating x and y
  MUL $x $top $bottom
  INVERT $inv_slope $slope
  MUL $inv_slope $inv_slope -1
  SUB $x_min_e $x $e
  MUL $y_one $inv_slope $x_min_e
  ADD $y $y_one $f

  #Calculating deltas
  SUB $a_min_x $a $x
  SUB $b_min_y $b $y
  SUB $e_min_x $e $x
  SUB $f_min_y $f $y

  POW $a_min_x_2 $a_min_x 2
  POW $b_min_y_2 $b_min_y 2
  ADD $delta_y_2 $a_min_x_2 $b_min_y_2
  POW $delta_y $delta_y_2 0.5

  ABS $abs_b_min_y $b_min_y
  INVERT $abs_b_min_y $abs_b_min_y
  MUL $sign_b_min_y $b_min_y $abs_b_min_y
  MUL $delta_y $sign_b_min_y $delta_y

  POW $e_min_x_2 $e_min_x 2
  POW $f_min_y_2 $f_min_y 2
  ADD $delta_x_2 $e_min_x_2 $f_min_y_2
  POW $delta_x $delta_x_2 0.5
  RETURN $delta_x $delta_y
